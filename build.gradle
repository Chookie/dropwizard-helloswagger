apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'idea'

// The main class of the application
mainClassName = 'com.example.helloworld.application.HelloSwaggerApplication'
version = '0.0.1-SNAPSHOT'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.7
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.7

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    // appends '-fat'
    classifier 'fat'

    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Implementation-Title': 'DropWizard Hello World Example',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '0.7.1'
    compile group: 'com.wordnik', name: 'swagger-jaxrs_2.11', version: '1.3.12'
    // For CORS
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '8.1.10.v20130312'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
